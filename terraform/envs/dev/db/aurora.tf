resource "aws_rds_cluster" "db" {
  enable_http_endpoint                = true # This enables Query Editor in the AWS RDS UI
  cluster_identifier                  = "${var.project}-${var.env}"
  database_name                       = "${var.project}_${var.env}"
  master_username                     = var.db_username
  master_password                     = module.db_password.value
  vpc_security_group_ids              = [aws_security_group.rds.id]
  db_subnet_group_name                = "${var.project}-${var.env}"
  storage_encrypted                   = true
  iam_database_authentication_enabled = true
  engine                              = "aurora-mysql"
  deletion_protection                 = !contains(["dev", "sandbox"], var.env)
  copy_tags_to_snapshot               = true
  backup_retention_period             = 7
  skip_final_snapshot                 = true

  db_cluster_parameter_group_name = aws_rds_cluster_parameter_group.db_8.id

  final_snapshot_identifier = "${var.project}-${var.env}-final"
}

resource "aws_rds_cluster_instance" "db" {
  count                      = 1
  identifier                 = "${var.project}-${var.env}-${count.index}"
  cluster_identifier         = aws_rds_cluster.db.id
  instance_class             = "db.r6g.large" # This was db.t3.medium, but needs to be larger to enable Query Editor in the AWS RDS UI
  db_subnet_group_name       = "${var.project}-${var.env}"
  db_parameter_group_name    = aws_db_parameter_group.db_8.name
  monitoring_interval        = 0
  auto_minor_version_upgrade = true
  ca_cert_identifier         = "rds-ca-ecc384-g1"
  engine                     = aws_rds_cluster.db.engine

  tags = {
    terraform = true
  }
}

resource "aws_rds_cluster_parameter_group" "db_8" {
  name        = "${var.project}-${var.env}-rds-cluster-pg-8"
  family      = "aurora-mysql8.0"
  description = "RDS default cluster parameter group"

  parameter {
    name  = "character_set_server"
    value = "utf8"
  }

  parameter {
    name  = "character_set_client"
    value = "utf8"
  }

  parameter {
    apply_method = "pending-reboot"
    name         = "binlog_format"
    value        = "ROW"
  }

  tags = {
    terraform = true
  }
}

resource "aws_db_parameter_group" "db_8" {
  name   = "${var.project}-${var.env}-rds-pg-8"
  family = "aurora-mysql8.0"

  parameter {
    name  = "general_log"
    value = "0"
  }

  parameter {
    name  = "slow_query_log"
    value = "1"
  }

  parameter {
    name  = "long_query_time"
    value = "2"
  }

  parameter {
    name         = "log_output"
    value        = "FILE"
    apply_method = "pending-reboot"
  }

  parameter {
    name  = "log_queries_not_using_indexes"
    value = "1"
  }

  # TODO: This got removed for some reason sometime April-December 2025; Re-added Feb 2026
  parameter {
    name  = "group_concat_max_len"
    value = "1073741824"
  }

  tags = {
    terraform = true
  }
}
