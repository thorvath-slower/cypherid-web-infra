locals {
  # env_fqdn    = data.terraform_remote_state.route53.outputs.env_seqtoid_org_fqdn
  full_domain = "${var.component}.${data.terraform_remote_state.route53.outputs.env_seqtoid_org_fqdn}"
  zone_id     = data.terraform_remote_state.route53.outputs.env_seqtoid_org_zone_id

  aliases = {
    "www.${local.full_domain}" = local.zone_id
  }
}

resource "aws_s3_bucket" "maintenance_bucket" {
  bucket        = local.full_domain
  acl           = "private"
  force_destroy = true

  website {
    index_document = "index.html"
    error_document = "index.html"
  }
}

data "aws_iam_policy_document" "s3_iam_policy" {
  statement {
    actions   = ["s3:GetObject"]
    resources = ["${aws_s3_bucket.maintenance_bucket.arn}/*"]

    principals {
      type        = "AWS"
      identifiers = [aws_cloudfront_origin_access_identity.origin_access_identity.iam_arn]
    }
  }
}

resource "aws_s3_bucket_policy" "s3_bucket_policy" {
  bucket = aws_s3_bucket.maintenance_bucket.id
  policy = data.aws_iam_policy_document.s3_iam_policy.json
}

module "assets-cert" {
  source = "github.com/chanzuckerberg/cztack//aws-acm-certificate?ref=v0.104.2"

  cert_domain_name               = local.full_domain
  aws_route53_zone_id            = local.zone_id
  cert_subject_alternative_names = local.aliases
  tags                           = var.tags # TODO: var.tags is deprecated

  # cloudfront requires us-east-1 acm certs
  providers = {
    aws = aws.us-east-1
  }
}

# module "env-cert" {
#   source = "github.com/chanzuckerberg/cztack//aws-acm-certificate?ref=v0.104.2"
#
#   cert_domain_name    = local.env_fqdn
#   aws_route53_zone_id = local.zone_id
#   tags                = var.tags # TODO: var.tags is deprecated
#
#   cert_subject_alternative_names = {
#     (local.full_domain)        = local.zone_id
#     "www.${local.full_domain}" = local.zone_id
#     "www.${local.env_fqdn}"   = local.zone_id
#   }
#   providers = {
#     aws = aws.us-east-1
#   }
# }

resource "aws_cloudfront_origin_access_identity" "origin_access_identity" {
  comment = "OAI for maintenance cloudfront distribution"
}

resource "aws_cloudfront_distribution" "distribution" {

  enabled             = true
  default_root_object = "index.html"
  comment             = "Serves ${var.env} maintenance page from S3 bucket"

  aliases = [local.full_domain]
  # aliases = [local.full_domain, local.env_fqdn]

  origin {
    domain_name = aws_s3_bucket.maintenance_bucket.bucket_regional_domain_name
    origin_id   = aws_s3_bucket.maintenance_bucket.bucket_regional_domain_name

    s3_origin_config {
      origin_access_identity = aws_cloudfront_origin_access_identity.origin_access_identity.cloudfront_access_identity_path
    }
  }

  custom_error_response {
    error_caching_min_ttl = 3600
    error_code            = 403
    response_code         = 503
    response_page_path    = "/index.html"
  }

  custom_error_response {
    error_caching_min_ttl = 3600
    error_code            = 404
    response_code         = 503
    response_page_path    = "/index.html"
  }

  default_cache_behavior {
    allowed_methods        = ["GET", "HEAD", "OPTIONS"]
    cached_methods         = ["GET", "HEAD"]
    target_origin_id       = aws_s3_bucket.maintenance_bucket.bucket_regional_domain_name
    viewer_protocol_policy = "redirect-to-https"

    default_ttl = 86400
    max_ttl     = 31536000
    min_ttl     = 0

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }
  }

  # Forward and cache assets requests to Rails web server.
  ordered_cache_behavior {
    path_pattern           = "/"
    allowed_methods        = ["GET", "HEAD", "OPTIONS"]
    cached_methods         = ["GET", "HEAD"]
    target_origin_id       = aws_s3_bucket.maintenance_bucket.bucket_regional_domain_name
    compress               = true
    viewer_protocol_policy = "redirect-to-https"

    default_ttl = 86400
    max_ttl     = 31536000
    min_ttl     = 0

    forwarded_values {
      # This is only necessary to set Vary: Origin header. See
      # https://stackoverflow.com/a/36585871 .
      headers = [
        "Origin",
      ]
      query_string = false

      cookies {
        forward = "none"
      }
    }
  }

  viewer_certificate {
    acm_certificate_arn = module.assets-cert.arn
    # acm_certificate_arn      = module.env-cert.arn
    ssl_support_method       = "sni-only"
    minimum_protocol_version = "TLSv1.1_2016"
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }
}

resource "aws_route53_record" "assets" {
  zone_id = local.zone_id
  name    = local.full_domain
  type    = "A"

  alias {
    name                   = aws_cloudfront_distribution.distribution.domain_name
    zone_id                = aws_cloudfront_distribution.distribution.hosted_zone_id
    evaluate_target_health = true
  }
}

# resource "aws_route53_record" "env_domain_maintenance_redirect" {
#   zone_id = local.zone_id
#   name    = local.env_fqdn
#   type    = "A"
#
#   alias {
#     name                   = aws_cloudfront_distribution.distribution.domain_name
#     zone_id                = aws_cloudfront_distribution.distribution.hosted_zone_id
#     evaluate_target_health = true
#   }
# }

# resource "aws_route53_record" "www_env_domain_maintenance_redirect" {
#   zone_id = local.zone_id
#   name    = "www.${local.env_fqdn}"
#   type    = "A"
#
#   alias {
#     name                   = aws_cloudfront_distribution.distribution.domain_name
#     zone_id                = aws_cloudfront_distribution.distribution.hosted_zone_id
#     evaluate_target_health = true
#   }
# }

locals {
  mime_types = {
    "html" = "text/html"
    "css"  = "text/css"
    "js"   = "application/javascript"
    "json" = "application/json"
    "png"  = "image/png"
    "jpg"  = "image/jpeg"
    "jpeg" = "image/jpeg"
  }
}

# Upload a single file
resource "aws_s3_object" "landing_page" {
  for_each = fileset("${path.module}/dist", "**/*")

  bucket = aws_s3_bucket.maintenance_bucket.id
  key    = each.value
  source = "${path.module}/dist/${each.value}"
  etag   = filemd5("${path.module}/dist/${each.value}")

  # Extracts the extension and looks up the MIME type, defaulting to octet-stream
  content_type = lookup(local.mime_types, regex("[^.]+$", each.value), "application/octet-stream")
}
